action (CopyCat) {
	type (Calculation)
	description (The main action that copies what you say.)
	collect {
		input (reset) {
			type (Reset)
			min (Optional) max (One)
			validate{
				if (!exists(input)){
					replan{
						intent{
							goal:Reset(false)
						}
					}
				}
			}
		}
		input (resetInput) {
			type (ResetInput)
			min (Optional) max (One)
			validate{
				if (!exists(input)){
					replan{
						intent{
							goal:ResetInput(false)
						}
					}
				}
			}
		}
		input (resetVoice) {
			type (ResetVoice)
			min (Optional) max (One)
		}
		input (input) {
			type (Input)
			min (Required) max (One)
		}
		input (voice) {
			type (Voice)
			min (Optional) max (One)
		}
	}
	output (Output)
	{
		throws {
			error (Reset) {
				on-catch {
					replan {
						dialog("Reset All Debug Message.")
						intent {
							goal:CopyCat
						}
					}
				}
			}
			error (ResetInput) {
				on-catch {
					replan {
						dialog("Reset Input Debug Message.")
						intent {
							goal:CopyCat
							value:$expr(voice)
						}
					}
				}
			}
			error (ResetVoice) {
				on-catch {
					replan {
						dialog("Reset Voice Debug Message.")
						intent {
							goal:CopyCat
							value:$expr(input)
							subplan {
								goal: GetVoices
							}
						}
					}
				}
			}
		}
	}
}
